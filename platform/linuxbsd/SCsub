#!/usr/bin/env python
from misc.utility.scons_hints import *

Import("env")

import os
import platform_linuxbsd_builders

common_linuxbsd = [
    "crash_handler_linuxbsd.cpp",
    "os_linuxbsd.cpp",
    "joypad_linux.cpp",
    "freedesktop_portal_desktop.cpp",
    "freedesktop_screensaver.cpp",
]

if env["use_sowrap"]:
    common_linuxbsd.append("xkbcommon-so_wrap.c")

if env["x11"]:
    common_linuxbsd += SConscript("x11/SCsub")

if env["wayland"]:
    common_linuxbsd += SConscript("wayland/SCsub")

if env["speechd"]:
    common_linuxbsd.append("tts_linux.cpp")
    if env["use_sowrap"]:
        common_linuxbsd.append("speechd-so_wrap.c")

if env["fontconfig"]:
    if env["use_sowrap"]:
        common_linuxbsd.append("fontconfig-so_wrap.c")

if env["udev"]:
    if env["use_sowrap"]:
        common_linuxbsd.append("libudev-so_wrap.c")

if env["dbus"]:
    if env["use_sowrap"]:
        common_linuxbsd.append("dbus-so_wrap.c")

linuxbsd_lib = env.add_library("linuxbsd", ["godot_linuxbsd.cpp"] + common_linuxbsd)

# remove system libs
godot_libs = [l for l in env['LIBS'] if not isinstance(l, str)]

def merge_libs(env, target, source):
    mri_script = f'create {target[0]}\n'
    for s in source:
        mri_script += f'addlib {s}\n'
    mri_script += 'save\nend'

    with os.popen('ar -M', mode='w') as p:
        p.write(mri_script)
        p.close()

prog = env.Command(f'#bin/libgodot{env["LIBSUFFIX"]}', godot_libs + [linuxbsd_lib], merge_libs)

if env["debug_symbols"] and env["separate_debug_symbols"]:
    env.AddPostAction(prog, env.Run(platform_linuxbsd_builders.make_debug_linuxbsd))
